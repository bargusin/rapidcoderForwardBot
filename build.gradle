plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'jacoco'
    id 'org.sonarqube' version '4.4.1.3373'
}

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    archiveBaseName = 'rapidcoderForwardBot'
    version = '0.1.0'

    manifest {
        attributes(
                'Main-Class': 'ru.rapidcoder.forward.bot.Main'
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

application {
    mainClass = 'ru.rapidcoder.forward.bot.Main'
}

dependencies {
    implementation 'org.slf4j:slf4j-api:2.0.17'
    implementation 'ch.qos.logback:logback-core:1.5.17'
    implementation 'ch.qos.logback:logback-classic:1.5.17'

    implementation 'org.telegram:telegrambots:6.9.7.1'
    implementation 'org.telegram:telegrambotsextensions:6.9.7.1'

    implementation 'org.xerial:sqlite-jdbc:3.45.1.0'
    implementation 'com.google.code.gson:gson:2.10.1'

    testImplementation 'junit:junit:4.13.2'
    testImplementation("org.junit.jupiter:junit-jupiter:5.9.3")
    testImplementation("org.mockito:mockito-core:5.3.1")
    testImplementation("org.mockito:mockito-junit-jupiter:5.3.1")
    testImplementation("org.assertj:assertj-core:3.24.2")
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }

    minHeapSize = "512m"
    maxHeapSize = "1024m"

    finalizedBy jacocoTestReport
}

checkstyle {
    toolVersion = '10.12.5'
    configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
}

jacoco {
    toolVersion = "0.8.10"
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.1 // 10% coverage
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true

        html.outputLocation = layout.buildDirectory.dir("${rootDir}/reports/jacoco/html")

        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        '**/config/**'
                ])
            }))
        }
    }
}

sonar {
    properties {
        property 'sonar.gradle.skipCompile', 'true'
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.organization', System.getenv('SONAR_ORGANIZATION')
        property 'sonar.projectKey', System.getenv('SONAR_PROJECT_KEY')
        //property 'sonar.sonarToken', System.getenv('SONAR_TOKEN')

        property 'sonar.sourceEncoding', 'UTF-8'
        property 'sonar.java.source', '17'
        property 'sonar.java.binaries', 'build/classes/java/main'
        property 'sonar.java.libraries', 'build/libs/*.jar'
        property 'sonar.coverage.jacoco.xmlReportPaths', 'build/reports/jacoco/test/jacocoTestReport.xml'
    }
}

// Зависимости между задачами
tasks.test {
    finalizedBy tasks.jacocoTestReport
}

tasks.check {
    dependsOn tasks.jacocoTestCoverageVerification
}
