plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'jacoco'
    id 'org.sonarqube' version '4.4.1.3373'
}

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

application {
    mainClass = 'ru.rapidcoder.forward.bot.Main'
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--abbrev=0'
            standardOutput = stdout
        }
        def tag = stdout.toString().trim()
        if (tag.startsWith('v')) {
            tag = tag.substring(1)
        }
        return tag ?: '0.0.1-SNAPSHOT'
    } catch (Exception e) {
        return '0.0.1-SNAPSHOT'
    }
}

def getBuildNumber = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--count', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (Exception e) {
        return '1'
    }
}

def getGitHash = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (Exception e) {
        return 'unknown'
    }
}

version = getVersionName()
def buildNumber = getBuildNumber()
def gitHash = getGitHash()

jar {
    archiveBaseName = 'rapidcoderForwardBot'
    archiveVersion = project.version

    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': "${version}-${buildNumber}",
                'Implementation-Build': buildNumber,
                'Git-Commit-Hash': gitHash,
                'Build-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Main-Class': application.mainClass.get()
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

dependencies {
    implementation("org.slf4j:slf4j-api:2.0.17")
    implementation("ch.qos.logback:logback-core:1.5.21")
    implementation("ch.qos.logback:logback-classic:1.5.21")

    implementation("org.telegram:telegrambots:6.9.7.1")
    implementation("org.telegram:telegrambotsextensions:6.9.7.1")

    implementation("org.xerial:sqlite-jdbc:3.50.3.0")

    testImplementation("junit:junit:4.13.2")
    testImplementation("org.junit.jupiter:junit-jupiter:5.9.3")
    testImplementation("org.mockito:mockito-core:5.8.0")
    testImplementation("org.mockito:mockito-junit-jupiter:5.8.0")
    testImplementation("org.assertj:assertj-core:3.24.2")
    testImplementation("org.awaitility:awaitility:4.2.0")
    testImplementation("uk.org.webcompere:system-stubs-jupiter:2.1.0")
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }

    minHeapSize = "512m"
    maxHeapSize = "1024m"

    finalizedBy jacocoTestReport
}

checkstyle {
    toolVersion = '10.12.5'
    configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
}

jacoco {
    toolVersion = "0.8.10"
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.1 // 10% coverage
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true

        html.outputLocation = layout.buildDirectory.dir("${rootDir}/reports/jacoco/html")

        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        '**/config/**'
                ])
            }))
        }
    }
}

sonar {
    properties {
        property 'sonar.gradle.skipCompile', 'true'
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.organization', System.getenv('SONAR_ORGANIZATION')
        property 'sonar.projectKey', System.getenv('SONAR_PROJECT_KEY')

        property 'sonar.sourceEncoding', 'UTF-8'
        property 'sonar.java.source', '17'
        property 'sonar.java.binaries', 'build/classes/java/main'
        property 'sonar.java.libraries', 'build/libs/*.jar'
        property 'sonar.coverage.jacoco.xmlReportPaths', 'build/reports/jacoco/test/jacocoTestReport.xml'
    }
}

check.dependsOn jacocoTestCoverageVerification
